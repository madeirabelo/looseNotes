# looseNotes
Some Ideas for me to track
WebFocus
Nr. TTKs distintos com interacções do NOC CorpNet

TABLE FILE rio_dwh_op_ptp/NOSSIS_OPIND_STT_ALTERACOES_AVARIAS_BV
PRINT
REF_TTK DH_CRIACAO_TTK EQUIPA_ALTERA_TTK UTILIZADOR_ALTERA_TTK DH_ALTERACAO_TTK_INFO_ALTERACOES
ESTADO_INICIAL_TTK ESTADO_FINAL_TTK OBSERVACOES_TTK
COMPUTE MES_CRIACAO_TTK/I2 = HPART(DH_CRIACAO_TTK, 'MONTH', 'I2');
COMPUTE MES_ALTERACAO_TTK/I2 = HPART(DH_ALTERACAO_TTK_INFO_ALTERACOES, 'MONTH', 'I2');
WHERE EQUIPA_ALTERA_TTK EQ 'NOC CorpNet';
WHERE DH_CRIACAO_TTK GE DT('2018/01/01');
-*ON TABLE PCHOLD FORMAT EXL07
ON TABLE HOLD AS LISTA_ALTERACOES_TTKS FORMAT XFOCUS INDEX REF_TTK
END
-RUN


TABLE FILE rio_dwh_op_ptp/NOSSIS_OPIND_STT_PARTICIPACOES_BV
PRINT
REF_TTK DH_CRIACAO_TTK REF_PARTICIP DH_CRIACAO_PARTICIP EQUIPA_CRIA_PARTICIP UTILIZADOR_CRIA_PARTICIP
EQUIPA_PARTICIPADA UTILIZADOR_PARTICIPADO 
COMPUTE MES_CRIACAO_TTK/I2 = HPART(DH_CRIACAO_TTK, 'MONTH', 'I2');
COMPUTE MES_CRIACAO_PARTICIP/I2 = HPART(DH_CRIACAO_PARTICIP, 'MONTH', 'I2');
WHERE EQUIPA_CRIA_PARTICIP EQ 'NOC CorpNet' OR EQUIPA_PARTICIPADA EQ 'NOC CorpNet';
WHERE DH_CRIACAO_TTK GE DT('2018/01/01');
ON TABLE HOLD AS LISTA_PARTICIPACOES_TTKS FORMAT XFOCUS INDEX REF_TTK
END
-RUN


TABLE FILE rio_dwh_op_ptp/NOSSIS_OPIND_STT_INTERVENCOES_BV
PRINT
REF_TTK DH_CRIACAO_TTK REF_INTERV DH_CRIACAO_INTERV EQUIPA_CRIA_INTERV UTILIZADOR_CRIA_INTERV
EQUIPA_INTERVENIENTE UTILIZADOR_INTERVENIENTE EQUIPA_FECHA_INTERV UTILIZADOR_FECHA_INTERV
COMPUTE MES_CRIACAO_TTK/I2 = HPART(DH_CRIACAO_TTK, 'MONTH', 'I2');
COMPUTE MES_CRIACAO_INTERV/I2 = HPART(DH_CRIACAO_INTERV, 'MONTH', 'I2');
WHERE EQUIPA_CRIA_INTERV EQ 'NOC CorpNet' OR EQUIPA_INTERVENIENTE EQ 'NOC CorpNet' OR EQUIPA_FECHA_INTERV EQ 'NOC CorpNet';
WHERE DH_CRIACAO_TTK GE DT('2018/01/01');
ON TABLE HOLD AS LISTA_INTERVENCOES_TTKS FORMAT XFOCUS INDEX REF_TTK
END
-RUN


TABLE FILE LISTA_ALTERACOES_TTKS
PRINT
DST.REF_TTK AS 'REF_TTK'
DST.MES_CRIACAO_TTK AS 'MES_CRIACAO_TTK'
ON TABLE HOLD AS LISTA_DST_TTKS FORMAT XFOCUS
ON TABLE SET HOLDLIST PRINTONLY
MORE
FILE LISTA_PARTICIPACOES_TTKS
MORE
FILE LISTA_INTERVENCOES_TTKS
END
-RUN


TABLE FILE LISTA_DST_TTKS
COUNT
REF_TTK AS 'NR_TTKS'
BY MES_CRIACAO_TTK 
ON TABLE SUBHEAD
"Nr. TTKs distintos com interacções do NOC CorpNet"
ON TABLE SET PAGE-NUM NOLEAD
ON TABLE PCHOLD FORMAT EXL2K OPEN
ON TABLE SET BYDISPLAY ON
ON TABLE SET HTMLCSS ON
ON TABLE SET HTMLENCODE ON
ON TABLE SET EMPTYREPORT ON
ON TABLE SET STYLE *
INCLUDE = endeflt, $
TYPE=REPORT, OBJECT=STATUS-AREA, PAGE-LOCATION=OFF, LINES-PER-PAGE=UNLIMITED, TITLETEXT=TTKS_MES, $
TYPE=TABHEADING, LINE=1, JUSTIFY=LEFT, $
TYPE=TABHEADING, LINE=1, ITEM=1, OBJECT=TEXT, SIZE=12, STYLE=BOLD+ITALIC, $
ENDSTYLE
END
-RUN
